ext {
    def coreBuildFilePath = 'https://raw.githubusercontent.com/grails/grails-core/3.0.x/build.gradle'
    def coreBuildFileUrl = new URL(coreBuildFilePath)
    def versionNumberRegex = /^\s*grailsVersion\s*=\s*['|"](.*)['|"]\s*$/
    def grailsVersionList = []
    coreBuildFileUrl.eachLine { line ->
        def matcher = (line =~ versionNumberRegex)
        if(matcher) {
            grailsVersionList << matcher[0][1]
        }
    }
    if(!grailsVersionList) {
        throw new GradleException("Could not find Grails version at $coreBuildFilePath")
    } else if(grailsVersionList.size() > 1) {
        throw new GradleException("Multiple Grails versions (${grailsVersionList}) found at $coreBuildFilePath")
    }
    grailsVersion = grailsVersionList[0]
}

subprojects {
    configurations.all {
        // check for updates every build
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }    
}